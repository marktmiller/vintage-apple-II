 10  REM  * VAR. SETUP *
 20  FOR B = 1 TO 10:DL$ = DL$ + " ": NEXT B: FOR B = 1 TO 79:TDL$ = TDL$ + " ": NEXT B: FOR B = 1 TO 40:PT$ = PT$ + "-": NEXT B: FOR B = 1 TO 7: READ DT$(B): NEXT B:DM = 20: DATA  SUN.,MON.,TUE.,WED.,THU.,FRI.,SAT.
 30  REM  * INTRO. *
 40 QJ = 1: HOME : PRINT " FLEXIBLE SYSTEMS SOFTWARE PRESENTS...": VTAB 2: PRINT "             WEEK-IN-ADVANCE": VTAB 3: HTAB 9: PRINT "BY MARK MILLER 2/10/88": VTAB 4: PRINT "MODE-->": PRINT PT$
 50 A = 4:FF = 0: FOR PR = 1 TO 7: GOSUB 1770: NEXT PR
 60  NORMAL : VTAB 8: HTAB 3: FOR B = 1 TO 7: PRINT  SPC( 1);: INVERSE : PRINT DT$(B);: NORMAL : NEXT B
 70  GOSUB 1880
 80  REM  * MAIN MENU *
 90  VTAB 14: HTAB 16: PRINT "MAIN MENU": VTAB 15: HTAB 16: PRINT "---------": VTAB 17: HTAB 4: PRINT "(1) ADD EVENTS": VTAB 18: HTAB 4: PRINT "(2) VIEW/EDIT A DAY": VTAB 19: HTAB 4: PRINT "(3) LOAD WEEK": VTAB 20: HTAB 4: PRINT "(4) SAVE WEEK"
 100  VTAB 21: HTAB 4: PRINT "(5) CLEAR A DAY": VTAB 17: HTAB 24: PRINT "(6) CLEAR WEEK": VTAB 18: HTAB 24: PRINT "(7) QUIT"
 110  GOSUB 1920: VTAB 4: HTAB 9: PRINT "SELECT": VTAB 22: PRINT TDL$: VTAB 22: INPUT CH$
 120  IF QJ = 1 THEN QJ = 0: VTAB 1: PRINT PT$: VTAB 3: PRINT  LEFT$ (TDL$,40): VTAB 22
 130  IF  VAL (CH$) < 1 OR  VAL (CH$) > 7 THEN 110
 140  IF  VAL (CH$) = 3 AND LF = 1 THEN  VTAB 21: PRINT  CHR$ (7): GOTO 110
 150  IF  VAL (CH$) = 6 THEN  VTAB 22: PRINT TDL$: VTAB 4: HTAB 9: PRINT "CLEAR ALL": VTAB 22: PRINT "ARE YOU SURE": INPUT R$: IF R$ = "Y" THEN 1530
 160  IF  VAL (CH$) = 6 THEN 110
 170  IF  VAL (CH$) = 7 THEN  HOME : END : REM  ** QUIT **
 180  GOSUB 1930
 190  IF  VAL (CH$) = 1 THEN 230
 200  IF  VAL (CH$) = 3 OR  VAL (CH$) = 4 THEN  GOSUB 1920: GOTO 250
 210  IF  VAL (CH$) = 5 THEN 240
 220  VTAB 22: PRINT TDL$: VTAB 22: PRINT "WHAT DAY DO WANT TO VIEW/EDIT": INPUT DAY$: GOTO 280
 230  VTAB 22: PRINT TDL$: VTAB 22: PRINT "WHAT DAY DO YOU WANT TO ADD EVENTS TO": INPUT DAY$: GOTO 280
 240  VTAB 22: PRINT TDL$: VTAB 22: PRINT "WHAT DAY DO YOU WANT TO CLEAR": INPUT DAY$: GOTO 280
 250  IF  VAL (CH$) = 3 THEN  VTAB 4: HTAB 9: PRINT "LOAD WEEK"
 260  IF  VAL (CH$) = 4 THEN  VTAB 4: HTAB 9: PRINT "SAVE WEEK"
 270  VTAB 22: PRINT TDL$: VTAB 22: PRINT "FILENAME:": INPUT DA$: IF DA$ = "" THEN  VTAB 21: PRINT  CHR$ (7): GOTO 270
 280  GOSUB 1910: GOSUB 450: IF QJ = 1 THEN QJ = 0: GOTO 110
 290  IF  VAL (CH$) = 3 THEN  VTAB 22: PRINT TDL$: GOTO 1440
 300  IF  VAL (CH$) = 4 THEN  VTAB 22: PRINT TDL$: GOTO 1390
 310  GOSUB 370: GOSUB 420: IF QJ = 1 THEN QJ = 0: GOTO 180
 320  IF  VAL (CH$) = 5 THEN 1510
 330  IF  VAL (CH$) = 1 AND CN(BQ) = DM THEN  VTAB 21: PRINT  CHR$ (7): GOTO 180
 340  REM  OPEN WINDOW
 350  GOSUB 1920:PR = BQ:FF = 1: GOSUB 1770: VTAB 8: HTAB C: PRINT  SPC( 4): GOSUB 1560: GOTO 470
 360  REM  * MORE VAR. SETUP, ERROR CHECKING *
 370 QJ = 1: FOR B = 1 TO 7
 380  IF DAY$ =  LEFT$ (DT$(B),3) THEN BQ = B:QJ = 0
 390  NEXT B
 400 CC = 4 + ((BQ - 1) * (A + 1)):LK = 12:TA = 0:BTA = 0:LA = 0:RA = 0:CBT = 0:TV = 4:TP = 1:BOT =  INT ((24 - LK) / TV):LEFT =  INT (CC / TV):RIGHT =  INT ((40 - (CC + 4)) / TV)
 410  RETURN 
 420  IF  VAL (CH$) = 1 THEN  RETURN 
 430  IF CN(BQ) = 0 THEN QJ = 1: PRINT  CHR$ (7)
 440  RETURN 
 450  IF DAY$ = "*" THEN QJ = 1
 460  RETURN 
 470  POKE 34,6: HOME :FF = 0: GOSUB 1750: VTAB 6: HTAB 2: INVERSE : PRINT ":";DT$(BQ);":": NORMAL : POKE 32,1: POKE 33,38: POKE 35,22: HOME 
 480  IF  VAL (CH$) = 2 THEN 640
 490  IF LF = 0 THEN  DIM EV$(20),DW$(7,20):LF = 1
 500  REM  ** ADD EVENTS **
 510 B = 0: GOSUB 1960: GOSUB 1930: VTAB 4: HTAB 9: PRINT "INPUT EVENTS": POKE 32,1: POKE 33,38: VTAB 7
 520 B = B + 1: INPUT EV$(B)
 530  IF EV$(B) = "*" THEN EV$(B) = "":QJ = 1: GOTO 590
 540 SP = 75 -  LEN (EV$(B)): IF SP =  < 0 THEN 560
 550 EV$(B) = EV$(B) +  LEFT$ (TDL$,SP)
 560  PRINT  LEFT$ (PT$,37)
 570  IF B + CN(BQ) = DM THEN  PRINT  CHR$ (7): GOTO 610
 580  IF QJ = 0 THEN 520
 590  IF B = 1 THEN B = 0:QJ = 0: GOTO 620
 600 QJ = 0:B = B - 1
 610  FOR AP = 1 TO B:CN(BQ) = CN(BQ) + 1:DW$(BQ,CN(BQ)) =  LEFT$ (EV$(AP),75): NEXT AP
 620  GOSUB 1960: GOTO 1730
 630  REM  ** VIEW/EDIT EVENTS **
 640 LI = 5: IF CN(BQ) < 5 THEN LI = CN(BQ)
 650 QJ = 0:RQ = 1: GOSUB 1960: GOSUB 1950: GOSUB 1940: POKE 32,1: POKE 33,38: VTAB 7: GOSUB 1890
 660  GET B$
 670  IF B$ =  CHR$ (21) THEN LI = LI + 1:EV = EV + 1:QJ = 0: GOTO 750
 680  IF B$ =  CHR$ (8) THEN LI = LI - 1:EV = EV - 1:QJ = 1: GOTO 750
 690  IF B$ = "E" THEN  GOSUB 1960: GOTO 820
 700  IF B$ = "I" AND CN(BQ) = DM THEN  PRINT  CHR$ (7):QJ = 1: GOSUB 780: GOTO 660
 710  IF B$ = "D" OR B$ = "I" THEN  GOSUB 1960: GOTO 820
 720  IF B$ = "P" THEN  GOSUB 1360
 730  IF B$ = "*" THEN  GOSUB 1960: GOTO 1730
 740  GOTO 660
 750  IF LI > CN(BQ) THEN LI = CN(BQ): GOTO 660
 760  GOSUB 770: GOTO 660
 770  IF QJ = 0 THEN  GOSUB 1860: RETURN 
 780 RQ = LI - 4
 790  IF RQ <  = 0 THEN EV = EV + 1:LI = LI + 1: RETURN 
 800  IF QJ = 1 THEN QJ = 0: GOSUB 1890: RETURN 
 810  REM  * EDIT MODE MENU, SCREEN SETUPS, GO TO APPROPRIATE EDITING FUNCTIONS *
 820 ZW = 5
 830  IF CN(BQ) < 5 THEN ZW = CN(BQ)
 840 AP = 0: INVERSE : FOR B = 7 TO 7 + ((ZW - 1) * 3) STEP 3:AP = AP + 1: VTAB B: HTAB 1: PRINT AP: NEXT B: NORMAL 
 850  IF B$ = "D" THEN  VTAB 4: HTAB 9: PRINT "EDIT/DELETE": GOTO 870
 860  IF B$ = "I" THEN  VTAB 4: HTAB 9: PRINT "EDIT/INSERT": GOTO 870
 870  VTAB 4: HTAB 9: PRINT "EDIT": VTAB 3: HTAB 1: PRINT  LEFT$ (TDL$,40)
 880  VTAB 3: PRINT "WHICH ONE (1-";ZW;")";: INPUT CH$:OLD = LI - 5 +  VAL (CH$)
 890  IF CN(BQ) < 5 THEN OLD =  VAL (CH$)
 900  IF OLD > CN(BQ) THEN 880
 910  IF  VAL (CH$) > CN(BQ) THEN 880
 920 AP = 0: INVERSE : FOR B = 7 TO 7 + ((ZW - 1) * 3) STEP 3:AP = AP + 1: VTAB B: HTAB 1: PRINT  SPC( 1): NEXT B: NORMAL 
 930  IF  VAL (CH$) = 0 THEN 1060
 940  REM  (DELETE AN EVENT)
 950  IF B$ = "D" THEN  FOR B = OLD TO CN(BQ):DW$(BQ,B) = DW$(BQ,B + 1):DW$(BQ,B + 1) = "": NEXT B:CN(BQ) = CN(BQ) - 1: IF LI > CN(BQ) THEN LI = CN(BQ)
 960  IF B$ = "D" THEN 1060
 970  IF B$ = "I" THEN 1010
 980  REM  SCREEN SETUP AND GO TO EDIT MODE
 990  VTAB 3: HTAB 1: PRINT "<-->=LEFT-RIGHT,CTRL-I=INS,CTRL-D=DEL": VTAB 4: HTAB 14: PRINT ": 'RETURN'=EXIT": GOSUB 1840: GOSUB 1100: GOTO 1060
 1000  REM  (INSERT AN EVENT)
 1010  FOR B = CN(BQ) TO OLD STEP  - 1:DW$(BQ,B + 1) = DW$(BQ,B):DW$(BQ,B) = "": NEXT B:CN(BQ) = CN(BQ) + 1
 1020  GOSUB 1840: VTAB 11: HTAB 1: INPUT OAD$
 1030  IF 75 -  LEN (OAD$) <  = 0 THEN 1050
 1040 OAD$ = OAD$ +  LEFT$ (TDL$,75 -  LEN (OAD$))
 1050  POKE 32,1: POKE 33,38: POKE 34,6: POKE 35,22:EV = OLD: GOSUB 1850
 1060  GOSUB 1960: GOSUB 1950: GOSUB 1940: POKE 32,1: POKE 33,38: POKE 34,6: POKE 35,22: VTAB 7:RQ = LI - 4: IF RQ =  < 0 THEN RQ = 1:LI = CN(BQ)
 1070  IF LI = 0 THEN  GOSUB 1960: GOTO 1730
 1080  GOSUB 1890: GOTO 660
 1090  REM  ** EDIT AN EVENT **
 1100 EV = OLD: HOME : PRINT DW$(BQ,EV): PRINT  LEFT$ (PT$,37):PL = 11:QE = 1:CE = 1:OLD$ = DW$(BQ,EV):OAD$ = OLD$:QJ = 0: GOTO 1160
 1110  REM  CURSOR POSITION CHECK
 1120  IF CE < 1 THEN CE = 1:QE = 1: GOTO 1160
 1130  IF CE > 75 THEN QE = 37:CE = 75: GOTO 1160
 1140  IF CE = 38 THEN PL = 11:QE = 38: GOTO 1160
 1150  IF CE = 39 THEN PL = 12:QE = 1
 1160  VTAB PL: HTAB QE: GET WE$
 1170  IF WE$ =  CHR$ (21) THEN CE = CE + 1:QE = QE + 1:QJ = 0: GOTO 1120
 1180  IF WE$ =  CHR$ (8) THEN CE = CE - 1:QE = QE - 1:QJ = 0: GOTO 1120
 1190  IF WE$ =  CHR$ (9) THEN QJ = 1: GOTO 1160
 1200  IF WE$ >  =  CHR$ (32) AND WE$ <  =  CHR$ (90) AND QJ = 1 THEN 1330
 1210  IF WE$ =  >  CHR$ (32) AND WE$ <  =  CHR$ (90) THEN 1300
 1220  IF WE$ =  CHR$ (4) THEN QJ = 0: GOTO 1260
 1230  IF  ASC (WE$) = 13 THEN  GOSUB 1850: RETURN 
 1240  GOTO 1160
 1250  REM  (DELETE CHARACTERS)
 1260  IF CE = 75 THEN OLD$ =  LEFT$ (OAD$,74) + " ":OAD$ = OLD$: VTAB PL: HTAB QE: PRINT " ": GOTO 1160
 1270  IF CE > 1 THEN OLD$ =  LEFT$ (OAD$,CE - 1) +  RIGHT$ (OAD$,75 - CE) + " ":OAD$ = OLD$: VTAB 11: HTAB 1: PRINT OAD$: GOTO 1120
 1280  IF CE = 1 THEN OLD$ =  RIGHT$ (OAD$,74) + " ":OAD$ = OLD$: VTAB 11: HTAB 1: PRINT OAD$: GOTO 1120
 1290  REM  (TEXT ENTRY/INSERT)
 1300  IF CE = 1 THEN OLD$ = WE$ +  RIGHT$ (OAD$,74):OAD$ = OLD$: VTAB PL: HTAB QE: PRINT WE$:CE = CE + 1:QE = QE + 1: GOTO 1120
 1310  IF CE = 75 THEN OLD$ =  LEFT$ (OAD$,74) + WE$:OAD$ = OLD$: VTAB PL: HTAB QE: PRINT WE$: GOTO 1160
 1320  IF CE > 1 THEN OLD$ =  LEFT$ (OAD$,CE - 1) + WE$ +  RIGHT$ (OAD$,75 - CE):OAD$ = OLD$: VTAB PL: HTAB QE: PRINT WE$:CE = CE + 1:QE = QE + 1: GOTO 1120
 1330  IF CE = 1 THEN OLD$ = WE$ +  LEFT$ (OAD$,74):OAD$ = OLD$: VTAB PL: HTAB QE: PRINT OAD$:CE = CE + 1:QE = QE + 1: GOTO 1120
 1340  IF CE > 1 THEN OLD$ =  LEFT$ (OAD$,CE - 1) + WE$ +  MID$ (OAD$,CE, LEN (OAD$) - (CE - 1) - 1):OAD$ = OLD$: VTAB 11: HTAB 1: PRINT OAD$:CE = CE + 1:QE = QE + 1: GOTO 1120
 1350  REM  * PRINT A DAY *
 1360  PR# 1: PRINT  CHR$ (9);"80N": PRINT  CHR$ (27);"W1": PRINT PT$: PRINT "::";DT$(BQ);":"; SPC( 32);":": PRINT ":"; LEFT$ (PT$,38);":"
 1370  FOR B = 1 TO CN(BQ): PRINT ":"; LEFT$ (DW$(BQ,B),38);":": PRINT ":"; RIGHT$ (DW$(BQ,B),37);" :": PRINT ":"; LEFT$ (PT$,38);":": NEXT B: PRINT PT$: PRINT  CHR$ (27);"W0": PR# 0: RETURN 
 1380  REM  ** SAVE WEEK **
 1390  PRINT  CHR$ (4);"OPEN";DA$: PRINT  CHR$ (4);"WRITE";DA$: FOR B = 1 TO 7: PRINT CN(B): NEXT B
 1400  FOR B = 1 TO 7: IF CN(B) = 0 THEN 1420
 1410  FOR AP = 1 TO CN(B): PRINT  CHR$ (34);DW$(B,AP): NEXT AP
 1420  NEXT B: PRINT  CHR$ (4);"CLOSE";DA$: GOTO 110
 1430  REM  ** LOAD WEEK **
 1440  PRINT  CHR$ (4);"OPEN";DA$: PRINT  CHR$ (4);"READ";DA$: FOR B = 1 TO 7: INPUT CN(B): NEXT B
 1450  FOR B = 1 TO 7: IF CN(B) > LG THEN LG = CN(B)
 1460  NEXT B: DIM DW$(7,LG + 20),EV$(LG + 20):DM = LG + 20
 1470  FOR B = 1 TO 7: IF CN(B) = 0 THEN 1490
 1480  FOR AP = 1 TO CN(B): INPUT DW$(B,AP): NEXT AP
 1490  NEXT B: PRINT  CHR$ (4);"CLOSE";DA$: GOSUB 1880:LF = 1: GOTO 110
 1500  REM  ** CLEAR A DAY **
 1510  FOR B = 1 TO CN(BQ):DW$(BQ,B) = "": NEXT B:CN(BQ) = 0: GOSUB 1880: GOTO 110
 1520  REM  ** CLEAR WEEK **
 1530  FOR B = 1 TO 7: FOR AP = 1 TO CN(B):DW$(B,AP) = "": NEXT AP:CN(B) = 0: NEXT B: GOSUB 1880: GOTO 110
 1540  REM  * MISC. SUBROUTINES *
 1550  REM  * WINDOW OPENING/CLOSING *
 1560  IF BB = 1 THEN TP =  - TP:BOT =  - BOT:LEFT =  - LEFT:RIGHT =  - RIGHT
 1570  FOR B = 1 TO TV - 1
 1580 TA = TA + TP:BTA = BTA + BOT:LA = LA + LEFT:RA = RA + RIGHT:TC = LK - 4 - TA:BC = 24 - LK + BTA:LC = CC - LA:RC = CC + 4 + RA
 1590  IF BB = 0 THEN  GOTO 1650
 1600  IF TC > 9 THEN TC = 9
 1610  IF BC < 12 THEN BC = 12
 1620  IF LC > CC THEN LC = CC
 1630  IF RC < CC + 4 THEN RC = CC + 4
 1640  GOTO 1690
 1650  IF TC < 6 THEN TC = 6
 1660  IF BC > 23 THEN BC = 23
 1670  IF LC < 1 THEN LC = 1
 1680  IF RC > 39 THEN RC = 39
 1690  VTAB TC: HTAB LC: PRINT  LEFT$ (PT$,RC - LC): FOR AP = TC + 1 TO BC - 1: VTAB AP: HTAB LC: PRINT ":": VTAB AP: HTAB RC - 1: PRINT ":": NEXT AP: VTAB BC: HTAB LC: PRINT  LEFT$ (PT$,RC - LC)
 1700  VTAB TC: HTAB LC: PRINT  LEFT$ (TDL$,RC - LC): FOR AP = TC + 1 TO BC - 1: VTAB AP: HTAB LC: PRINT  SPC( 1): VTAB AP: HTAB RC - 1: PRINT  SPC( 1): NEXT AP: VTAB BC: HTAB LC: PRINT  LEFT$ (TDL$,RC - LC)
 1710  NEXT B: RETURN 
 1720  REM  * CLOSE WINDOW, RETURN TO MAIN SCREEN *
 1730 BB = 1:FF = 1: HOME : POKE 32,0: POKE 33,40: POKE 34,0: POKE 35,24: GOSUB 1750: GOSUB 1560:BB = 0: VTAB 8: HTAB CC: INVERSE : PRINT DT$(BQ):FF = 0:PR = BQ: GOSUB 1770: GOTO 50
 1740  REM  DRAW FINAL FRAME OF WINDOW
 1750  GOSUB 1800: VTAB 6: PRINT  SPC( 40): FOR B = 7 TO 23: VTAB B: HTAB 1: PRINT  SPC( 1): HTAB 40: PRINT  SPC( 1): NEXT B: VTAB 23: HTAB 1: PRINT  SPC( 40): NORMAL : RETURN 
 1760  REM  * ERASE/DRAW A 'DAY-BOX' *
 1770 C = 4 + ((PR - 1) * (A + 1))
 1780  GOSUB 1800: VTAB 9: HTAB C: PRINT  SPC( 4): VTAB 10: HTAB C: PRINT " ";: NORMAL : PRINT "  ";: GOSUB 1800: PRINT " ": VTAB 11: HTAB C: PRINT " ";: HTAB C + 3: PRINT " ": VTAB 12: HTAB C: PRINT  SPC( 4): RETURN 
 1790  REM  * CHANGE PRINT MODE *
 1800  IF FF = 0 THEN  INVERSE 
 1810  IF FF = 1 THEN  NORMAL 
 1820  RETURN 
 1830  REM  DRAW EDIT WINDOW
 1840  POKE 34,10: POKE 35,14: POKE 33,38: POKE 32,1: HOME : VTAB 10: HTAB 1: INVERSE : PRINT "EDIT EVENT#:";OLD; SPC( 25): VTAB 15: PRINT  SPC( 40): NORMAL : RETURN 
 1850 DW$(BQ,EV) =  LEFT$ (OAD$,75): RETURN : REM  STORE AN EVENT
 1860  PRINT DW$(BQ,EV): PRINT  LEFT$ (PT$,37): RETURN : REM  PRINT EVENT (IN WINDOW)
 1870  REM  * PUT NUMBER OF EVENTS IN 'DAY-BOXES' *
 1880  FOR B = 1 TO 7:VT = 4 + ((B - 1) * (A + 1)) + 1: VTAB 10: HTAB VT: PRINT "  ": VTAB 10: HTAB VT: PRINT CN(B): NEXT B: RETURN 
 1890  HOME : FOR B = RQ TO LI:EV = B: GOSUB 1860: NEXT B:LI = B - 1: RETURN : REM  * DISPLAY EVENTS IN VIEW WINDOW *
 1900  REM  * SCREEN FORMATTING ROUTINES *
 1910  VTAB 3: HTAB 1: PRINT  LEFT$ (TDL$,40): RETURN 
 1920  VTAB 4: HTAB 9: PRINT DL$: RETURN 
 1930  VTAB 3: HTAB 1: PRINT "*=EXIT": RETURN 
 1940  VTAB 3: PRINT "*=EXIT,<-->=UP-DOWN,E=EDIT,D=DEL,I=INS": VTAB 4: HTAB 14: PRINT " : P=PRINT": RETURN 
 1950  VTAB 4: HTAB 9: PRINT "VIEW": RETURN 
 1960  POKE 32,0: POKE 33,40: GOSUB 1910: GOSUB 1920: GOSUB 1970: RETURN 
 1970  VTAB 4: HTAB 14: PRINT  SPC( 15): RETURN 
